[FormDesignerId("ED75D0A6-0050-4AA3-8DB8-692530041C95")]
[ClassId("084CBF41-4738-4139-85F0-153A67C30615")]
[InterfaceId("7ACC8E18-0503-4379-8251-7B36996E1E82")]
[EventInterfaceId("B500051D-EAB1-47FB-80A9-6A83ED13D625")]
Class frmMain
    Attribute VB_Name = "frmMain"
    Attribute VB_GlobalNameSpace = False
    Attribute VB_Creatable = False
    Attribute VB_PredeclaredId = True
    Attribute VB_Exposed = False
    Option Explicit
    
    Dim cD2dFactory As ID2D1Factory
    Dim cTarget     As ID2D1HwndRenderTarget
    Dim cDWFactory  As IDWriteFactory
    Dim cTextFormat As IDWriteTextFormat
    Dim cTextlayout As IDWriteTextLayout
    Dim sText       As String
    Dim cCustRender As CRenderer
    
    Private Sub Form_Load()

        sText = "Custom renderer example twinBASIC"
        
        Set cD2dFactory = D2D1.CreateFactory
        
        Set cTarget = cD2dFactory.CreateHwndRenderTarget(D2D1.RenderTargetProperties( _
                                        D2D1.PixelFormat(DXGI_FORMAT_B8G8R8A8_UNORM)), _
                                        D2D1.HwndRenderTargetProperties(Me.hWnd, D2D1.SizeU))
                                        
        Set cDWFactory = DW.CreateFactory(DWRITE_FACTORY_TYPE_SHARED)
        Set cTextFormat = cDWFactory.CreateTextFormat(StrPtr("Arial"), Nothing, DWRITE_FONT_WEIGHT_NORMAL, _
                                        DWRITE_FONT_STYLE_NORMAL, DWRITE_FONT_STRETCH_NORMAL, _
                                        32# * 96# / 72#, StrPtr("en-US"))
                                        
        Set cTextlayout = cDWFactory.CreateTextLayout(StrPtr(sText), Len(sText), cTextFormat, Me.ScaleWidth, Me.ScaleHeight)
        
        Dim tRange As DWRITE_TEXT_RANGE
        tRange.startPosition = 0: tRange.Length = 6
        cTextlayout.SetFontFamilyName StrPtr("Courier New"), tRange
        tRange.startPosition = 7: tRange.Length = 8
        cTextlayout.SetFontFamilyName StrPtr("Impact"), tRange
        
        tRange.startPosition = 24: tRange.Length = 6
        cTextlayout.SetFontStyle DWRITE_FONT_STYLE_ITALIC, tRange
        tRange.startPosition = 31: tRange.Length = 6
        cTextlayout.SetFontWeight DWRITE_FONT_WEIGHT_BOLD, tRange
        cTextlayout.SetParagraphAlignment DWRITE_PARAGRAPH_ALIGNMENT_CENTER
        cTextlayout.SetTextAlignment DWRITE_TEXT_ALIGNMENT_CENTER
        
        tRange.startPosition = 0: tRange.Length = 6
        cTextlayout.SetStrikethrough 1, tRange
        tRange.startPosition = 31: tRange.Length = 5
        cTextlayout.SetUnderline 1, tRange
        
        Set cCustRender = New CRenderer
        
        cCustRender.Initialize cTarget, cD2dFactory
                               
        ' // Create effect
        Dim cEffect As CColorEffect
        
        Set cEffect = New CColorEffect
        
        cEffect.Color = D2D1_COLORS.Green
        
        tRange.startPosition = 0: tRange.Length = 6
        cTextlayout.SetDrawingEffect cEffect, tRange
                            
        Set cEffect = New CColorEffect
        
        cEffect.Color = D2D1_COLORS.HotPink
        
        tRange.startPosition = 24: tRange.Length = 8
        cTextlayout.SetDrawingEffect cEffect, tRange
        
    End Sub
    
    Private Sub Form_Paint()
        
        cTarget.BeginDraw
        
        cTarget.Clear D2D1.ColorF(Ivory)
        
        cTextlayout.Draw ByVal 0, cCustRender, 10, 10
        
        cTarget.EndDraw ByVal 0, ByVal 0
        
    End Sub
    
    Private Sub Form_Resize()
        
        cTextlayout.SetMaxWidth Me.ScaleWidth
        cTextlayout.SetMaxHeight Me.ScaleHeight
        
        cTarget.Resize D2D1.SizeU(Me.ScaleWidth, Me.ScaleHeight)
        
        Form_Paint
        
    End Sub
    

End Class
